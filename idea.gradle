apply plugin: 'org.jetbrains.intellij'

/**
 * @see org.jetbrains.intellij.IntelliJPluginExtension
 */
intellij {
    pluginName = PLUGIN_NAME
    // https://plugins.jetbrains.com/docs/intellij/tools-gradle-intellij-plugin.html#configuration-intellij-extension
    type = PLATFORM_TYPE
    // https://plugins.jetbrains.com/docs/intellij/plugin-compatibility.html
    version = PLATFORM_VERSION
    // https://plugins.jetbrains.com/docs/intellij/plugin-dependencies.html
    plugins = PLATFORM_PLUGINS.split(',').collect { it.trim() }.findAll { !it.isEmpty() }
}

/**
 * @see org.jetbrains.intellij.tasks.PatchPluginXmlTask
 */
patchPluginXml {
    // https://plugins.jetbrains.com/docs/intellij/build-number-ranges.html
    sinceBuild = PLUGIN_SINCE_BUILD
    untilBuild = PLUGIN_UNTIL_BUILD
}

/**
 * @see org.jetbrains.intellij.tasks.PublishPluginTask
 */
publishPlugin {
    def channel = "default"
    if (VERSION.contains("-")) {
        channel = VERSION.substring(VERSION.indexOf("-") + 1)
    }
    token = System.getenv("PUBLISH_TOKEN")
    // The pluginVersion is based on the SemVer (https://semver.org) and supports pre-release labels, like 2.1.7-alpha.3
    // Specify pre-release label to publish the plugin in a custom Release Channel automatically. Read more:
    // https://plugins.jetbrains.com/docs/intellij/deployment.html#specifying-a-release-channel
    channels = [channel]
}
